/*
 * Copyright Terracotta, Inc.
 * Copyright IBM Corp. 2024, 2025
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.ehcache.build.conventions.java'
}

dependencies {
  testImplementation project(':clustered:ehcache-client')
  testImplementation project(':clustered:ehcache-common')
  testImplementation project(':ehcache-impl')
  testImplementation project(':ehcache-xml')
  testImplementation project(':ehcache-107')
  testImplementation "org.terracotta.internal:client-runtime:$terracottaRuntimeVersion"
  testImplementation "org.terracotta:runnel:$terracottaPlatformVersion"
  testImplementation "org.terracotta:lease-api:$terracottaPlatformVersion"
  testImplementation("javax.cache:cache-tests:$jcacheTckVersion") {
    exclude group:'junit', module:'junit'
  }
  testImplementation("javax.cache:cache-tests:$jcacheTckVersion:tests") {
    exclude group:'junit', module:'junit'
  }

  testImplementation project(':ehcache-management')
  testImplementation "org.terracotta.management:nms-entity-client:$terracottaPlatformVersion"
  testImplementation "org.terracotta.management:nms-agent-entity-client:$terracottaPlatformVersion"
  testImplementation "org.terracotta:terracotta-utilities-port-chooser:$terracottaUtilitiesVersion"
  testImplementation("org.terracotta.internal:galvan-support:$terracottaRuntimeVersion") {
    exclude group: 'org.terracotta', module: 'terracotta-utilities-port-chooser'
    exclude group: 'org.slf4j'
  }
  testImplementation("org.terracotta.dynamic-config.cli:dynamic-config-cli-upgrade-tools:$terracottaPlatformVersion")
  testImplementation "javax.cache:cache-api:$jcacheVersion"
  testImplementation ("ch.qos.logback:logback-classic:1.2.13") {
    exclude group: 'org.slf4j'
  }
  testImplementation("org.terracotta.dynamic-config.cli:upgrade-tools-oss:$terracottaPlatformVersion")
}

task unzipKit(type: Sync) {
  dependsOn project(':clustered:ehcache-clustered').distZip
  from zipTree(project(':clustered:ehcache-clustered').distZip.archivePath)
  into layout.buildDirectory.dir("ehcache-kit")
}

task copyBaseKit() {
    dependsOn(unzipKit)
    outputs.dir(layout.buildDirectory.dir("plugin/api"))
    outputs.dir(layout.buildDirectory.dir("plugin/lib"))
    doLast {
        project.sync {
            from layout.buildDirectory.dir("ehcache-kit/ehcache-clustered-${project.version}-kit/server/plugins/api")
            into layout.buildDirectory.dir("plugin/api")
        }
        project.sync {
            from layout.buildDirectory.dir("ehcache-kit/ehcache-clustered-${project.version}-kit/server/plugins/lib")
            into layout.buildDirectory.dir("plugin/lib")
        }
    }
}

test {
  maxHeapSize = '512m'
  maxParallelForks = 8
  dependsOn copyBaseKit
  environment 'JAVA_HOME', testJava.javaHome
  //If this directory does not exist, tests will fail with a cryptic assert failure
    systemProperty "galvan.plugin", layout.buildDirectory.dir("plugin").get().toString()
    systemProperty "galvan.dir", layout.buildDirectory.dir("galvan").get().toString()
    systemProperty "galvan.server", layout.buildDirectory.dir("tmp/tcserver").get().toString()
//    systemProperty "serverDebugPortStart", 9000
  // Uncomment to include client logging in console output
  // testLogging.showStandardStreams = true
}

configurations.all {
  resolutionStrategy {
    dependencySubstitution {
      substitute(module('junit:junit:4.12'))
        .because('CVE-2020-15250')
        .with(module('junit:junit:4.13.1'))
    }
  }
}

